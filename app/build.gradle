plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'

    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlinx-serialization'

    id 'com.google.gms.google-services'
}

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

android {
    compileSdk Publishing.compileSdk

    defaultConfig {
        testInstrumentationRunnerArguments suppressErrors: 'DEBUGGABLE,NOT-PROFILEABLE'
        multiDexEnabled true
        applicationId Publishing.applicationId
        minSdkVersion Publishing.minSdk
        targetSdkVersion Publishing.targetSdk
        versionCode Publishing.versionCode
        versionName Publishing.publishVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/proguard/*']
        }
        resources {
            excludes += ['META-INF/*.version', 'META-INF/proguard/*', '/*.properties', 'fabric/*.properties', 'META-INF/*.properties']
        }
    }


    buildTypes {
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            debuggable true
        }

        benchmark {
            signingConfig signingConfigs.debug
            debuggable false
        }

        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            signingConfig = signingConfigs.debug
            proguardFiles 'proguard-rules.pro'
        }
    }
    namespace 'org.kafka.user'
    lint {
        abortOnError true
        checkDependencies true
        checkReleaseBuilds false
        ignoreTestSources true
    }
}

dependencies {
    implementation project(':common:ui-common')
    implementation project(':common:ui-common-theme')
    implementation project(':common:ui-components')
    implementation project(':base-domain')
    implementation project(':data')
    implementation project(':navigation')
    implementation project(':analytics')
    implementation project(':notifications')
    implementation project(':navigation')
    implementation project(':domain')
    implementation project(':ui-homepage')
    implementation project(':ui-search')
    implementation project(':ui-item')
    implementation project(':ui-reader')
    implementation project(':ui-favorites')
    implementation project(':ui-artist')
    implementation project(':webview')
    implementation project(':playback-core')
    implementation project(':playback-ui')

    implementation "androidx.metrics:metrics-performance:1.0.0-alpha03"

    implementation platform(libs.google.bom)

    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-dynamic-links-ktx"

//    implementation libs.pdftron.standard
//    implementation libs.pdftron.tools

    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx

    implementation libs.compose.foundation.foundation
    implementation libs.compose.foundation.layout
    implementation libs.compose.animation.animation
    implementation libs.compose.ui.tooling
    implementation libs.compose.ui.ui
    implementation libs.compose.ui.util
    implementation libs.compose.livedata

    implementation libs.coil.coil

    implementation libs.pdfkit
    implementation libs.androidx.appcompat

    implementation libs.androidx.activity.compose

    implementation libs.retrofit.serialization
    implementation libs.kotlin.serialization
    implementation libs.okhttp.okhttp
    implementation libs.okhttp.loggingInterceptor

    implementation libs.threeTenAbp
    implementation libs.dataStore

    implementation libs.accompanist.navigation.animation
    implementation libs.accompanist.navigation.material
    api libs.accompanist.pager.core

    implementation libs.timber
    debugImplementation libs.leakCanary
    debugImplementation libs.leakCanary.process

    api libs.tracing

    implementation libs.kotlin.stdlib

    implementation libs.kotlin.coroutines.core
    implementation libs.kotlin.coroutines.android

    implementation libs.hilt.library
    kapt libs.hilt.compiler

    implementation libs.androidx.hilt.navigation
    implementation libs.androidx.hilt.compose
    kapt libs.androidx.hilt.compiler

    implementation libs.icons.feather
    implementation libs.icons.tabler
}

android.applicationVariants.all { variant ->
    task "open${variant.name.capitalize()}" {
        dependsOn "install${variant.name.capitalize()}"

        doLast {
            exec {
                commandLine "adb shell monkey -p -t ${variant.applicationId} -c android.intent.category.LAUNCHER 1".split(" ")
            }
        }
    }
}

//apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
